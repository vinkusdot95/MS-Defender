//Detect users that tries to remove managed.json to "tampering" protection
// MDE on linux already detects modification in to managed.json file but not removal
// works with MDE attached devices, but also with no mdeattached ones if you replace the good file name in to line 7

DeviceProcessEvents
| where Timestamp > ago(1h)
| join kind=inner (
    DeviceInfo
    | where MachineGroup == "DG-WK_Linux" //if needed define devicegroup that contains linux devices
    | summarize arg_max(Timestamp, *) by DeviceId  // deduplicate by latest info per device
    | project DeviceId, MachineGroup
) on DeviceId
| where ProcessCommandLine has "mdeattach_managed.json"
| where FileName in~ (
    "rm", "unlink", "shred", "find", "perl", "python", "bash", "mv", "cp", "echo", "dd", "truncate"
)
// remove duplicates by relevant fields
| distinct Timestamp, DeviceName, DeviceId, MachineGroup, ReportId, InitiatingProcessAccountName, FileName, ProcessCommandLine, FolderPath, InitiatingProcessFileName
