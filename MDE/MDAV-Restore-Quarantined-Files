//detect usage of MpCmdRun.exe -Restore => users trying to restore malware from quarantine"
//GUI restore are not available using MDE telemetry, at this time we should use event viewer to find out the event id 1009 in defender (Applications and Services Logs / Microsoft /Windows/Windows Defender/Operationnal)
// meaning using sentinel or other SIEM tool
DeviceProcessEvents
| where Timestamp > ago(1h)
| where FileName =~ "MpCmdRun.exe"
| where ProcessCommandLine has "-Restore"
| project Timestamp,DeviceName,InitiatingProcessAccountName,FileName,ProcessCommandLine,FolderPath,SHA1,DeviceId,ReportId

//Detect usage of GUI to restore malware : Sentinel + ama + DCR are prerequisites + ARC for on premises server
// XPATH string to get all mdav event : Microsoft-Windows-Windows Defender/Operational!*
Event
| where EventLog == "Microsoft-Windows-Windows Defender/Operational"
| where EventID == "1009"
| extend RawData = tostring(EventData)
| extend User = extract("(?i)<Data Name=\"User\">(.*?)</Data>", 1, RawData)
| extend FileName = extract("(?i)<Data Name=\"Path\">(.*?)</Data>", 1, RawData)
| extend ThreatName = extract("(?i)<Data Name=\"Threat Name\">(.*?)</Data>", 1, RawData)
| project TimeGenerated, Computer, User, FileName, ThreatName
